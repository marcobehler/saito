buildscript {
    repositories {
        maven {
            url 'http://repo.marcobehler.com/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

def versionPropsFile = file("${rootDir}/version.properties")
def versionBuild = 0

if (versionPropsFile.canRead()) {
    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))
    versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
    versionProps['VERSION_BUILD'] = versionBuild.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
}

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"
}


subprojects {

    apply plugin: "net.ltgt.apt"
    apply plugin: 'java'
    apply plugin: 'idea'


    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            // generatedSourceDir does  not work for some reason
            sourceDirs += file(project.projectDir.absolutePath + '/src/main/generated')
        }
    }


    dependencies {
        apt 'com.google.dagger:dagger-compiler:2.2'
        apt 'org.projectlombok:lombok:1.16.8'

        compile 'org.projectlombok:lombok:1.16.8'

        compile 'com.google.dagger:dagger:2.2'

        compile 'org.projectlombok:lombok:1.16.8'

        compile 'javax.inject:javax.inject:1'

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.0.44-beta'
        testCompile 'org.assertj:assertj-core:3.4.0'
    }


    version = '1.0.' + versionBuild
    group = 'com.marcobehler'

    jar {
        manifest.attributes provider: 'gradle'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}

